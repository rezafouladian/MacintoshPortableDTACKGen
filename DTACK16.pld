Name        DTACK16;
PartNo      00;
Date        07/21/2023
Revision    01;
Designer    rezafouladian;
Company     None;
Assembly    None;
Location    None;
Device      g16v8;


PIN 1           =   CLOCK;                      /* 16MHz clock from the Macintosh Portable */
PIN [2..5]      =   [A23..20];                  /* Four address lines for address decoding */
PIN 6           =   !AS;                        /* The Address Strobe of the Macintosh Portable 68HC000 */
PIN [7..9]      =   [J4..6];                    /* Jumpers 4-6 */
PIN [12..14]    =   [J3..1];                    /* Jumpers 1-3 */
PIN 15          =   !DTACK;                     /* The Data Transfer Acknowledge of the Macintosh Portable 68HC000 */
PIN [16..18]    =   [DELAY1..3];                /* Pins used for D Flip-flops */
PIN 19          =   DTACK_SEL;                  /* Additional pin used because of product term limit */

FIELD ADDRESS   =   [A23..20];                  /* Expand the four address lines to 24-bit addresses */

/* Define memory address ranges */
PERMRAM         =   ADDRESS:[000000..0FFFFF];   /* Permanent 1MB RAM */
EXPRAM1         =   ADDRESS:[100000..4FFFFF];   /* 1MB to 5MB */
EXPRAM2         =   ADDRESS:[500000..8FFFFF];   /* 5MB to 9MB */
PERMROM         =   ADDRESS:[900000..9FFFFF];   /* Permanent 1MB ROM */
EXPROM          =   ADDRESS:[A00000..DFFFFF];   /* Expansion ROM space */
PDSROM          =   ADDRESS:[E00000..EFFFFF];   /* PDS ROM space */

/* Jumper setting mapping for readability */
DELAYPLUS1      =   J6;                         /* Add one clock pulse to the delay */
DELAYPLUS2      =   J5;                         /* Add two clock pulses to the delay */
SELPERM         =   J4;                         /* Generate DTACK when the permanent 1MB RAM is selected */
SELEXP1         =   J3;                         /* Generate DTACK when 1MB to 5MB is selected */
SELEXP2         =   J2;                         /* Generate DTACK when 5MB to 9MB is selected */
/* Jumper 1 is not currently used */


DTACK_SEL       =   (PERMRAM & SELPERM)         /* Selected if in the permanent RAM and jumper enabled */
                #   (EXPRAM1 & SELEXP1)         /* Selected if in 1MB to 5MB of RAM and jumper enabled */
                #   (EXPRAM2 & SELEXP2)         /* Selected if in 5MB to 9MB of RAM and jumper enabled */         
                &   AS;                         /* Only while AS is active indicating a valid address */

DTACK.OE        =   DTACK_SEL;                  /* Tri-state: Enable output when selected, otherwise Hi-Z */

DELAY1.D        =   DTACK_SEL & DELAYPLUS1 # DELAYPLUS2;    /* Delay for one clock pulse */
DELAY2.D        =   DTACK_SEL & DELAY1 & DELAYPLUS2;        /* Delay for two clock pulses */
DELAY3.D        =   DTACK_SEL & DELAY2 & DELAYPLUS1;        /* Delay for three clock pulses */

DTACK           =   (!DELAYPLUS1 & !DELAYPLUS2)             /* Generate DTACK immediately if no delay is enabled */
                #   (DELAYPLUS1 & !DELAYPLUS2 & DELAY1)     /* or after one clock pulse */
                #   (!DELAYPLUS1 & DELAYPLUS2 & DELAY2)     /* or after two clock pulses */
                #   (DELAYPLUS1 & DELAYPLUS2 & DELAY3)      /* or after three clock pulses */
                &   DTACK_SEL;                              /* Only while selected */